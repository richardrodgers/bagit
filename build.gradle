/*
 * Build file for BagIt library.
 * Copyright 2013, 2014 MIT Libraries
 * For those who prefer maven-based builds, to bootstrap:
 * uncomment the line below, run 'gradle install', take the
 * pom-default.xml generated in build/poms, and go in peace.
 */

apply plugin: 'java'
//apply plugin: 'maven'

repositories {
    mavenCentral()
}

sourceCompatibility = 11

group = 'edu.mit.lib'
archivesBaseName = 'bagit'
version = '1.0'
description = 'Compact Java BagIt library'

ext {
    developerName1 = "Richard Rodgers"
    developerId1 = "http://orcid.org/0000-0003-1412-5595"
    developerName2 = "Helen Bailey"
    developerId2 = "http://orcid.org/0000-0002-1881-2045"
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.15'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task fatJar(type: Jar) {
    manifest { attributes("Main-Class": "edu.mit.lib.bagit.Bagger") }
    baseName = archivesBaseName + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
